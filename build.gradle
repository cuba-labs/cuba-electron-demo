buildscript {
    ext.cubaVersion = '6.7.6'
    repositories {
        maven {
            url 'https://repo.cuba-platform.com/content/groups/work'
            credentials {
                username(rootProject.hasProperty('repoUser') ? rootProject['repoUser'] : 'cuba')
                password(rootProject.hasProperty('repoPass') ? rootProject['repoPass'] : 'cuba123')
            }
        }
        
    }
    dependencies {
        classpath "com.haulmont.gradle:cuba-plugin:$cubaVersion"
    }
}

def globalModule = project(':app-global')
def coreModule = project(':app-core')
def guiModule = project(':app-gui')
def webModule = project(':app-web')

// add electron module
def electronModule = project(':app-electron')

def servletApi = 'org.apache.tomcat:tomcat-servlet-api:8.0.26'


apply(plugin: 'idea')
apply(plugin: 'cuba')

cuba {
    artifact {
        group = 'com.company.sales'
        version = '0.1'
        isSnapshot = true
    }
    tomcat {
        dir = "$project.rootDir/deploy/tomcat"
    }
}

dependencies {
    appComponent("com.haulmont.cuba:cuba-global:$cubaVersion")

}

def hsql = 'org.hsqldb:hsqldb:2.2.9'

configure([globalModule, coreModule, guiModule, webModule]) {
    apply(plugin: 'java')
    apply(plugin: 'maven')
    apply(plugin: 'idea')
    apply(plugin: 'cuba')

    dependencies {
        testCompile('junit:junit:4.12')
    }

    task sourceJar(type: Jar) {
        from file('src')
        classifier = 'sources'
    }

    artifacts {
        archives sourceJar
    }
}

configure(globalModule) {
    task enhance(type: CubaEnhancing)

    
}

configure(coreModule) {

    configurations {
        jdbc
        dbscripts
    }

    dependencies {
        compile(globalModule)
        provided(servletApi)
        jdbc(hsql)
        testRuntime(hsql)

    }

    task cleanConf(description: 'Cleans up conf directory') {
        doLast {
            def dir = new File(cuba.tomcat.dir, '/conf/app-core')
            if (dir.isDirectory()) {
                ant.delete(includeemptydirs: true) {
                    fileset(dir: dir, includes: '**/*', excludes: 'local.app.properties')
                }
            }
        }
    }

    task deploy(dependsOn: [assemble, cleanConf], type: CubaDeployment) {
        appName = 'app-core'
        appJars('app-global', 'app-core')
    }

    task createDb(dependsOn: assembleDbScripts, description: 'Creates local database', type: CubaDbCreation) {
        dbms = 'hsql'
        host = 'localhost'
        dbName = 'sales'
        dbUser = 'sa'
        dbPassword = ''
    }

    task updateDb(dependsOn: assembleDbScripts, description: 'Updates local database', type: CubaDbUpdate) {
        dbms = 'hsql'
        host = 'localhost'
        dbName = 'sales'
        dbUser = 'sa'
        dbPassword = ''
    }

    task startTestDb(type: CubaHsqlStart) {
        dbPort = 9002 // should be different from the default port 9001 used by Studio HSQLDB server
        dbName = 'sales_test'
    }

    task createTestDb(dependsOn: assemble, description: 'Creates local Postgres database for tests', type: CubaDbCreation) {
        dbms = 'hsql'
        host = 'localhost:9002'
        dbName = 'sales_test'
        dbUser = 'sa'
        dbPassword = ''
    }
}

configure(guiModule) {
    dependencies {
        compile(globalModule)

    }

    task deployConf(type: Copy) {
        from file('src')
        include "com/company/sales/**"
        into "$cuba.tomcat.dir/conf/app"
    }
}

configure(webModule) {
    configurations {
        webcontent
    }

    dependencies {
        provided(servletApi)
        compile(guiModule)

    }
    
    task webArchive(type: Zip) {
        from file("$buildDir/web")
        from file('web')
        classifier = 'web'
    }

    artifacts {
        archives webArchive
    }

    task deployConf(type: Copy) {
        from file('src')
        include "com/company/sales/**"
        into "$cuba.tomcat.dir/conf/app"
    }

    task clearMessagesCache(type: CubaClearMessagesCache) {
        appName = 'app'
    }
    deployConf.dependsOn clearMessagesCache

    task cleanConf(description: 'Cleans up conf directory') {
        doLast {
            def dir = new File(cuba.tomcat.dir, '/conf/app')
            if (dir.isDirectory()) {
                ant.delete(includeemptydirs: true) {
                    fileset(dir: dir, includes: '**/*', excludes: 'local.app.properties')
                }
            }
        }
    }

    task deploy(dependsOn: [assemble, cleanConf], type: CubaDeployment) {
        appName = 'app'
        appJars('app-global', 'app-gui', 'app-web')
    }
}

configure(electronModule) {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'com.moowork.node'

    sourceSets {
        main {
            resources {
                srcDir 'src'
            }
        }
    }

    node {
        version = '8.9.1'
        download = true
        nodeModulesDir = project.file('src')
    }

    npm_install {
        workingDir = project.file('src')
    }

    task buildJsWrapper(dependsOn: npm_install) {
        inputs.dir(project.file('src'))
        outputs.dir(project.file('build/electron-wrapper'))

        doLast {
            def osName = System.getProperty('os.name').toLowerCase()

            def isWindows = osName.contains('windows')

            def localNodeInstallDir = node.workDir.listFiles().find {
                it.name.startsWith('node-') && it.name.contains(node.version)
            }
            def nodeDir = isWindows ? localNodeInstallDir : new File(localNodeInstallDir, 'bin')

            def targetDir = project.file('build/electron-wrapper')
            if (targetDir.exists()) {
                targetDir.deleteDir()
            }

            exec {
                workingDir 'src'
                environment 'PATH', System.getenv('PATH') + File.pathSeparator + nodeDir.absolutePath
                if (isWindows) {
                    commandLine 'cmd.exe', '/c', '"./node_modules/.bin/electron-packager" . ' +
                            '--out=../build/tmp/electron ' +
                            '--ignore=README.md ' +
                            '--ignore=.npmignore ' +
                            '--ignore=.travis.yml" .'
                } else {
                    commandLine './node_modules/.bin/electron-packager', '.',
                            '--out=../build/tmp/electron ',
                            '--ignore=README.md',
                            '--ignore=.npmignore',
                            '--ignore=.travis.yml'
                }
            }

            def files = project.file('build/tmp/electron').listFiles()
            if (files) {
                for (f in files) {
                    if (f.name.startsWith('cuba-electron-demo')) {
                        f.renameTo(targetDir)
                        break
                    }
                }
            }
        }
    }

    compileJava.enabled = false
    processResources.enabled = false
    jar.enabled = false

    assemble.dependsOn buildJsWrapper
}

task undeploy(type: Delete, dependsOn: ':app-web:cleanConf') {
    delete("$cuba.tomcat.dir/shared")
    delete("$cuba.tomcat.dir/webapps/app-core")
    delete("$cuba.tomcat.dir/webapps/app")
}

task restart(dependsOn: ['stop', ':app-core:deploy', ':app-web:deploy'], description: 'Redeploys applications and restarts local Tomcat') {
    doLast {
        ant.waitfor(maxwait: 6, maxwaitunit: 'second', checkevery: 2, checkeveryunit: 'second') {
            not {
                socket(server: 'localhost', port: '8787')
            }
        }
        start.execute()
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.3.1'
}

apply from: 'extra.gradle'

task buildUberJar(type: CubaUberJarBuilding) {
    webPort = 30550 // use non-standard port for electron app
    corePort = 8080
    coreJettyEnvPath = 'modules/core/web/META-INF/jetty-env.xml'
    appProperties = ['cuba.automaticDatabaseUpdate' : true]
}

task buildElectronApp(dependsOn: [buildUberJar, electronModule.buildJsWrapper]) {
    outputs.dir project.file('build/electron-app')

    doLast {
        def targetDir = project.file('build/electron-app')
        if (targetDir.exists()) {
            targetDir.deleteDir()
        }

        // copy electron
        copy {
            from electronModule.file('build/electron-wrapper')
            into project.file('build/electron-app')
        }

        // copy uberjar
        copy {
            from project.file('build/distributions/uberJar/app.jar')
            into project.file('build/electron-app/resources/app')
        }
    }
}